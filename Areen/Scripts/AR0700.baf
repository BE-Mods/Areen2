// This is the new block for the Promenade area, where Areen appears.
// The list of areas for the game is here: http://iesdp.gibberlings3.net/appendices/area_lists/bg2aref.htm
// It's pretty simple: if variable is 0, spawn Aren and set variable at 1.
// Don't forget Continue() at the end. It's needed when you add to the top(EXTEND_TOP in Setup-Areen.tp2) of the script.

IF
Global("O#ArenSpawn","GLOBAL",0)
THEN
RESPONSE #100
CreateCreature("BEAren",[2552.502],13)  // To choose your own coordinates, choose a spot in the game and press CTRL-X
SetGlobal("O#ArenSpawn","GLOBAL",1)
Continue()
END


// These blocks are adding experience only once, when he's in party.
// When Areen joins, if PC's experience is less, than 280000, then Branwen's experience will be at 161000.
// For PC between 280001 and 600000 points of experience, Areen's experience is going to become 400000.
// For PC between 600001 and 1000000 points of experience, Areen's experience is going to become 800000.
// For PC with more than 1000000 points of experience, Areen's experience is going to become 1200000.

IF
InParty("BEAren")
Global("BEArenExperienceSet","GLOBAL",0)
XPLT(Player1,280001)
THEN
RESPONSE #100
SetGlobal("BEArenExperienceSet","GLOBAL",1)
Continue()
END

IF
InParty("BEAren")
Global("BEArenExperienceSet","GLOBAL",0)
XPGT(Player1,280000)
XPLT(Player1,600001)
THEN
RESPONSE #100
SetGlobal("BEArenExperienceSet","GLOBAL",1)
AddXPObject("BEAren",239000)
Continue()
END

IF
InParty("BEAren")
Global("BEArenExperienceSet","GLOBAL",0)
XPGT(Player1,600000)
XPLT(Player1,1000001)
THEN
RESPONSE #100
SetGlobal("BEArenExperienceSet","GLOBAL",1)
AddXPObject("BEAren",639000)
Continue()
END

IF
InParty("BEAren")
Global("BEArenExperienceSet","GLOBAL",0)
XPGT(Player1,1000000)
THEN
RESPONSE #100
SetGlobal("BEArenExperienceSet","GLOBAL",1)
AddXPObject("BEAren",1039000)
Continue()
END
