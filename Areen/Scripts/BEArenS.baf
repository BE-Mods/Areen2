// Areen doesn't know level 7 Improved Chaos Shield, but a level 5 version, this ensures he doesn't get it back after reload/death
IF
  InParty(Myself)
  HaveKnownSpell(2723)
THEN
    RESPONSE #100
    ReallyForceSpellRES("BEDEL#3",Myself)
END

// Areen can practice magic and extend license  BUT I do not know how to reverse it yet.
IF
InParty(Myself)
GlobalGT("CowledWarning","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("CowledWarning","GLOBAL",10)
END

// Kruin gives a ring in return for the silver blade
 /*
IF
InParty(Myself)
Global("BEArenKRUINGloves", "GLOBAL", 1)
THEN
RESPONSE #100
GiveItemCreate("BEGLOVES",Player1,0,0,0)
SetGlobal("BEArenKRUINGloves", "GLOBAL", 2)
END
 */

// This is where Branwen's friendship talks start. O#BranShutUp is a variable you can set in player-initiated dialogue by telling Branwen you'd like some quiet, or asking her to talk to you again.
// So, when Branwen joins your party, a timer is set for one hour, and the first talk is preparing to run.
// ATTENTION: in all scripts InParty(Myself) should go first or right after Global() conditions if present. It means NPC is present and not dead. If your NPC is dead and this condition is not present, your game might crash.
IF 
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)	 // O#BranShutUp is used in player-initiated dialogue. Look it up.
Global("BEArenTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("BEArenTimer","GLOBAL",3600)
SetGlobal("BEArenTalk","GLOBAL",1)       // Each block must run only once, which is also why we set O#BranTalk to 1 - it means the same set of conditions will never be repeated again
END

// And this is how we initiate romance:
IF 
InParty(Myself)
//Gender(Player1,MALE)
OR(4)				// OR(4) means that four lines after OR() are a part of this OR() command.
Race(Player1,HUMAN)
Race(Player1,ELF)
Race(Player1,HALF_ELF)
Race(Player1,HALFORC)
Global("BEArenLovetalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenLovetalk","GLOBAL",1)
SetGlobal("BEArenRomanceActive","GLOBAL",1)
END

// This is a huge block. What does it do? It runs FIFTEEN talks at once! 
// (If it scares you, go below and see some simpler blocks, like Branwen's talk for Chapter 4).
// Let's see how it's done:

IF
InParty(Myself)
RealGlobalTimerExpired("BEArenTimer","GLOBAL")
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)    // CamDawg's condition, means that everyone can talk and is not charmed, confused, silenced and so on
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)                          // These two lines mean "no battle"
!See([ENEMY])
!AreaType(DUNGEON)			  // No talks in dungeons, right?
OR(15)                                    // Be careful here: 15 must mean EXACTLY 15 lines included in the condition. The 16th line will not be included!
Global("BEArenTalk","GLOBAL",1)
Global("BEArenTalk","GLOBAL",3)
Global("BEArenTalk","GLOBAL",5)
Global("BEArenTalk","GLOBAL",7)
Global("BEArenTalk","GLOBAL",9)
Global("BEArenTalk","GLOBAL",11)
Global("BEArenTalk","GLOBAL",13)
Global("BEArenTalk","GLOBAL",15)
Global("BEArenTalk","GLOBAL",17)
Global("BEArenTalk","GLOBAL",19)
Global("BEArenTalk","GLOBAL",21)
Global("BEArenTalk","GLOBAL",23)
Global("BEArenTalk","GLOBAL",25)
Global("BEArenTalk","GLOBAL",27)
Global("BEArenTalk","GLOBAL",29)
THEN
RESPONSE #100
IncrementGlobal("BEArenTalk","GLOBAL",1)
END

// So, if timer expired, everybody's able to talk and there's no battle, we increase the variable by one. Now our O#BranTalk variable is at 2, or 4, or 6... or 20, or 30.
// EXACTLY like the condition in the beginning of each talk, right?
// Now all that's left is making Branwen talk to you. We could just add StartDialogueNoSet(Player1) into the last block, but the engine is tricky: if you give a command when Branwen's about to talk, she may miss her dialogue!
// So we add another block. Actually, two blocks: one for the romanced Branwen and one for the non-romanced, because I want to play the music only if Branwen is romanced.
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
!Global("BEArenRomanceActive","GLOBAL",1)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(15)                                    // Again, OR(15) means that fifteen lines below are a part of OR() command
Global("BEArenTalk","GLOBAL",2)
Global("BEArenTalk","GLOBAL",4)
Global("BEArenTalk","GLOBAL",6)
Global("BEArenTalk","GLOBAL",8)
Global("BEArenTalk","GLOBAL",10)
Global("BEArenTalk","GLOBAL",12)
Global("BEArenTalk","GLOBAL",14)
Global("BEArenTalk","GLOBAL",16)
Global("BEArenTalk","GLOBAL",18)
Global("BEArenTalk","GLOBAL",20)
Global("BEArenTalk","GLOBAL",22)
Global("BEArenTalk","GLOBAL",24)
Global("BEArenTalk","GLOBAL",26)
Global("BEArenTalk","GLOBAL",28)
Global("BEArenTalk","GLOBAL",30)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Same block, romanced version(with music)
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
Global("BEArenRomanceActive","GLOBAL",1)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(15)                                    // Again, OR(15) means that fifteen lines below are a part of OR() command
Global("BEArenTalk","GLOBAL",2)
Global("BEArenTalk","GLOBAL",4)
Global("BEArenTalk","GLOBAL",6)
Global("BEArenTalk","GLOBAL",8)
Global("BEArenTalk","GLOBAL",10)
Global("BEArenTalk","GLOBAL",12)
Global("BEArenTalk","GLOBAL",14)
Global("BEArenTalk","GLOBAL",16)
Global("BEArenTalk","GLOBAL",18)
Global("BEArenTalk","GLOBAL",20)
Global("BEArenTalk","GLOBAL",22)
Global("BEArenTalk","GLOBAL",24)
Global("BEArenTalk","GLOBAL",26)
Global("BEArenTalk","GLOBAL",28)
Global("BEArenTalk","GLOBAL",30)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")                     // You can play your song here. Just create a wav file(for example, AAAA.wav, but it really must be prefixed with your prefix), and place its name here.
StartDialogueNoSet(Player1)
END

// In short, the long block you have just read means: if we're not fighting, are not in a dungeon and everyone's okay, Branwen will talk to me whenever her O#BranTalk value hits 2,4,6,8,10,12...30.

// Now let's check shorter and simple blocks, for one talk only. 
// Branwen has talks for Chapter 4,5 and 7.
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("Chapter","GLOBAL",%bg2_chapter_4%)            // that's the actual condition: Chapter=4. No dungeon check, since we're in Brynnlaw
Global("BEArenChapter4","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenChapter4","GLOBAL",1)
END

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("Chapter","GLOBAL",%bg2_chapter_5%)           // No dungeon check, since we're in the Underdark. Which is a big dungeon.
Global("BEArenChapter5","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenChapter5","GLOBAL",1)
RealSetGlobalTimer("BEArenChapter5Timer","GLOBAL",1800)    // setting a timer for another talk
END

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("Chapter","GLOBAL",%bg2_chapter_7%)           // No dungeon check, since we're in Suldanessellar.
Global("BEArenChapter7","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenChapter7","GLOBAL",1)
END

// So, if we're in Chapter 4, for example, then O#BranChapter4=1 and a talk from O#BranJ.d will run.
// Again, music plays if Branwen is romanced, doesn't play otherwise.
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(3)
Global("BEArenChapter4","GLOBAL",1)
Global("BEArenChapter5","GLOBAL",1)
Global("BEArenChapter7","GLOBAL",1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

// Same block, non-romanced version(without music)
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
!Global("BEArenRomanceActive","GLOBAL",1)
!Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(3)
Global("BEArenChapter4","GLOBAL",1)
Global("BEArenChapter5","GLOBAL",1)
Global("BEArenChapter7","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Extra talk: Sahuagin city
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR2300")           // Sahuagin City
Global("BEArenTalkSahuagin","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenTalkSahuagin","GLOBAL",1)
END

// Extra talk: an hour after Chapter 5
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
RealGlobalTimerExpired("BEArenChapter5Timer","GLOBAL") // Timer from Chapter 5 block expired
Global("BEArenTalkSoul","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenTalkSoul","GLOBAL",1)
END

// Running extra talks. Romance - with music, no romance - without music.
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(2)
Global("BEArenTalkSoul","GLOBAL",1)
Global("BEArenTalkSahuagin","GLOBAL",1)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
!Global("BEArenRomanceActive","GLOBAL",1)
!Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(2)
Global("BEArenTalkSoul","GLOBAL",1)
Global("BEArenTalkSahuagin","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Branwen's 3rd lovetalk after rest
IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",5)
PartyRested()
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
IncrementGlobal("BEArenLovetalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",6)
See(Player1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)  // No music here, because music from the previous lovetalk is probably still playing
END

// Why do we run each talk in two blocks? Why do we first set a variable and then run a block with StartDialogueNoSet? Well, there's a tutorial I wrote for that, How to make your banters run when you want them to. But short answer is: you will miss lovetalks if you give a command to your follower. You will miss lovetalks if your computer is too fast and you party's on the move as your follower wants to talk. It doesn't happen often, but when it does, it's frustrating. Hence, this "division" of blocks.

// Branwen's 4th lovetalk, an hour after the last one:
IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",7)
RealGlobalTimerExpired("BEArenLoveTimer","GLOBAL")
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
IncrementGlobal("BEArenLovetalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",8)
See(Player1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("O#BRAN")
StartDialogueNoSet(Player1)
END

// What else are we missing? Romance!
// If Aerie or Viconia or Jaheira love PC, Branwen should surrender.
IF 
OR(3)
Global("AerieRomanceActive","GLOBAL",2)
Global("JaheiraRomanceActive","GLOBAL",2)
Global("ViconiaRomanceActive","GLOBAL",2)
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenAnotherRomanceWon","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenRomanceActive","GLOBAL",3)
SetGlobal("BEArenAnotherRomanceWon","GLOBAL",1)
END

// And if Branwen's romance is at 2, Aerie, Viconia and Jaheira lose.
IF 
Global("BEArenRomanceActive","GLOBAL",2)
Global("AerieRomanceActive","GLOBAL",1)
Global("BEArenAerieRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("AerieRomanceActive","GLOBAL",3)
SetGlobal("BEArenAerieRomanceLost","GLOBAL",1)
END

IF 
Global("BEArenRomanceActive","GLOBAL",2)
Global("JaheiraRomanceActive","GLOBAL",1)
Global("BEArenJaheiraRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("JaheiraRomanceActive","GLOBAL",3)
SetGlobal("BEArenJaheiraRomanceLost","GLOBAL",1)
END

IF 
Global("BEArenRomanceActive","GLOBAL",2)
Global("ViconiaRomanceActive","GLOBAL",1)
Global("BEArenViconiaRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("ViconiaRomanceActive","GLOBAL",3)
SetGlobal("BEArenViconiaRomanceLost","GLOBAL",1)
END

// Branwen is romanceable, and she is going to initiate flirts!
// The blocks seem a bit heavy, but if you read them line by line, you'll be all right.

// Flirts initiate...
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
GlobalGT("BEArenTalk","GLOBAL",8)               // Flirts only start after Branwen's fourth talk
GlobalGT("BEArenLovetalk","GLOBAL",2)           // Flirts only start after Branwen's first lovetalk
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenFlirtsStart","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenFlirtsStart","GLOBAL",1)
RealSetGlobalTimer("BEArenFlirtTimer","GLOBAL",300)
END

// Ready for flirting!
IF
InParty(Myself)
RealGlobalTimerExpired("BEArenFlirtTimer","GLOBAL")
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
CombatCounter(0)
!See([ENEMY])
!Global("Chapter","GLOBAL",%bg2_chapter_5%)                   // No flirting in the Underdark.
Global("BEArenFlirt","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenFlirt","GLOBAL",1)
END

// Flirting begins!
IF
InParty(Myself)
Global("BEArenFlirt","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Note that there are two dialogue blocks for Branwen-initiated flirts in O#BranJ.d - for O#BranRomanceActive=1 and for O#BranRomanceActive=2.

// If Branwen becomes a vampire, here's her post-vampire dialogue:
IF
Global("BEArenVampire","GLOBAL",4)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END