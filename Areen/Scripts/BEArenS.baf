// Areen doesn't know level 7 Improved Chaos Shield, but a level 5 version, this ensures he doesn't get it back after reload/death

IF
InParty(Myself)
HaveKnownSpell(2723)
THEN
RESPONSE #100
ReallyForceSpellRES("BEDEL#3",Myself)    // this will trigger a cowled wizard to spawn, but this is accounted below.
END

// Areen can practice magic and extend license, the reverse needs checking

IF
InParty(Myself)
GlobalGT("CowledWarning","GLOBAL",0)
GlobalLT("CowledWarning","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("CowledWarning","GLOBAL",10)
END

IF
Global("BEArenKickedOut","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("CowledWarning","GLOBAL",1)
END

// Friendship talks start.

IF 
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
Global("BEArenTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("BEArenTimer","GLOBAL",2400)    // timer for talks
SetGlobal("BEArenTalk","GLOBAL",1)
END

// And this is how we initiate romance:

IF
InParty(Myself)
Gender(Player1,FEMALE)
OR(3)
Race(Player1,HUMAN)
Race(Player1,ELF)
Race(Player1,HALF_ELF)
Global("BEArenLovetalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenLovetalk","GLOBAL",1)
SetGlobal("BEArenRomanceActive","GLOBAL",1)
END

// Romance run

IF
InParty(Myself)
RealGlobalTimerExpired("BEArenTimer","GLOBAL")
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(15)
Global("BEArenTalk","GLOBAL",1)
Global("BEArenTalk","GLOBAL",3)
Global("BEArenTalk","GLOBAL",5)
Global("BEArenTalk","GLOBAL",7)
Global("BEArenTalk","GLOBAL",9)
Global("BEArenTalk","GLOBAL",11)
Global("BEArenTalk","GLOBAL",13)
Global("BEArenTalk","GLOBAL",15)
Global("BEArenTalk","GLOBAL",17)
Global("BEArenTalk","GLOBAL",19)
Global("BEArenTalk","GLOBAL",21)
Global("BEArenTalk","GLOBAL",23)
Global("BEArenTalk","GLOBAL",25)
Global("BEArenTalk","GLOBAL",27)
Global("BEArenTalk","GLOBAL",29)
THEN
RESPONSE #100
IncrementGlobal("BEArenTalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
!Global("BEArenRomanceActive","GLOBAL",1)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(15)
Global("BEArenTalk","GLOBAL",2)
Global("BEArenTalk","GLOBAL",4)
Global("BEArenTalk","GLOBAL",6)
Global("BEArenTalk","GLOBAL",8)
Global("BEArenTalk","GLOBAL",10)
Global("BEArenTalk","GLOBAL",12)
Global("BEArenTalk","GLOBAL",14)
Global("BEArenTalk","GLOBAL",16)
Global("BEArenTalk","GLOBAL",18)
Global("BEArenTalk","GLOBAL",20)
Global("BEArenTalk","GLOBAL",22)
Global("BEArenTalk","GLOBAL",24)
Global("BEArenTalk","GLOBAL",26)
Global("BEArenTalk","GLOBAL",28)
Global("BEArenTalk","GLOBAL",30)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Same block, romanced version(music not included yet)
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
Global("BEArenRomanceActive","GLOBAL",1)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(15)
Global("BEArenTalk","GLOBAL",2)
Global("BEArenTalk","GLOBAL",4)
Global("BEArenTalk","GLOBAL",6)
Global("BEArenTalk","GLOBAL",8)
Global("BEArenTalk","GLOBAL",10)
Global("BEArenTalk","GLOBAL",12)
Global("BEArenTalk","GLOBAL",14)
Global("BEArenTalk","GLOBAL",16)
Global("BEArenTalk","GLOBAL",18)
Global("BEArenTalk","GLOBAL",20)
Global("BEArenTalk","GLOBAL",22)
Global("BEArenTalk","GLOBAL",24)
Global("BEArenTalk","GLOBAL",26)
Global("BEArenTalk","GLOBAL",28)
Global("BEArenTalk","GLOBAL",30)
THEN
RESPONSE #100
PlaySong(0)
PlaySong(62)
StartDialogueNoSet(Player1)
END

// Extra talk: at the begining of Chapter 5

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("Chapter","GLOBAL",%bg2_chapter_5%)
Global("BEArenChapter5","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenChapter5","GLOBAL",1)
RealSetGlobalTimer("BEArenChapter5Timer","GLOBAL",90)    // setting a timer for another talk
END

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
RealGlobalTimerExpired("BEArenChapter5Timer","GLOBAL") // Timer from Chapter 5 talk expired
Global("BEArenTalkSoul","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenTalkSoul","GLOBAL",1)
END

// Running extra talks. Romance - with music, no romance - without music.
IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("BEArenTalkSoul","GLOBAL",1)
THEN
RESPONSE #100
PlaySong(0)
PlaySong(62)
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
Global("BEArenShutUp","GLOBAL",0)
!Global("BEArenRomanceActive","GLOBAL",1)
!Global("BEArenRomanceActive","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("BEArenTalkSoul","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// 3rd lovetalk after rest

IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",5)
PartyRested()
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
IncrementGlobal("BEArenLovetalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",6)
See(Player1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)  // No music here, because music from the previous lovetalk is probably still playing
END

// 4th lovetalk, an hour after the last one:

IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",7)
RealGlobalTimerExpired("BEArenLoveTimer","GLOBAL")
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
IncrementGlobal("BEArenLovetalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("BEArenLovetalk","GLOBAL",8)
See(Player1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenShutUp","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySong(62)
StartDialogueNoSet(Player1)
END

// post-vampire dialogue

IF
Global("BEArenVampire","GLOBAL",4)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// romance exclusivities

// What else are we missing? Romance!
// If Hexxat, Dorn, Anomen, Rasaad, Areen should surrender.
IF 
OR(4)
Global("AnomenRomanceActive","GLOBAL",2)
Global("HexxatRomanceActive","GLOBAL",2)
Global("RasaadRomanceActive","GLOBAL",2)
Global("DoornRomanceActive","GLOBAL",2)
OR(2)
Global("BEArenRomanceActive","GLOBAL",1)
Global("BEArenRomanceActive","GLOBAL",2)
Global("BEArenAnotherRomanceWon","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEArenRomanceActive","GLOBAL",3)
SetGlobal("BEArenAnotherRomanceWon","GLOBAL",1)
END

// And if Areen's romance is at 2, others surrender.
IF 
Global("BEArenRomanceActive","GLOBAL",2)
Global("AnomenRomanceActive","GLOBAL",1)
Global("BEArenAnomenRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("AnomenRomanceActive","GLOBAL",3)
SetGlobal("BEArenAnomenRomanceLost","GLOBAL",1)
END

IF
Global("BEArenRomanceActive","GLOBAL",2)
Global("AnomenRomanceActive","GLOBAL",1)
Global("BEArenHexxatRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("HexxatRomanceActive","GLOBAL",3)
SetGlobal("BEArenHexxatRomanceLost","GLOBAL",1)
END

IF
Global("BEArenRomanceActive","GLOBAL",2)
Global("DornRomanceActive","GLOBAL",1)
Global("BEArenDornRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("DornRomanceActive","GLOBAL",3)
SetGlobal("BEArenDornRomanceLost","GLOBAL",1)
END

IF
Global("BEArenRomanceActive","GLOBAL",2)
Global("RasaadRomanceActive","GLOBAL",1)
Global("BEArenRasaadRomanceLost","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("RasaadRomanceActive","GLOBAL",3)
SetGlobal("BEArenRasaadRomanceLost","GLOBAL",1)
END


// QUEST RELATED SCRIPTS

// Party enters graveyard and areen asks to go se tomb
IF
InParty(Myself)
Global("BEArenQuest","GLOBAL",0)
AreaCheck("AR0800")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("BEArenQuest","GLOBAL",1)
StartDialogueNoSet(Player1)
END

// Spawn Thayze in tomb

IF
InParty(Myself)
AreaCheck("AR0807")
Global("BEArenQuest","GLOBAL",2)
Global("BEThayzeSpawn","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEThayzeSpawn","GLOBAL",1)
CreateCreature("BESELEM",[360.302],13)
END

// Spawn Lord Selemchant in Councilg bulding

IF
InParty(Myself)
AreaCheck("AR1002")
Global("BEArenQuest","GLOBAL",3)
Global("BELordSelemchantSpawn","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BELordSelemchantSpawn","GLOBAL",1)
CreateCreature("BELORDSE",[1136.941],13)
END

// Spawn Acolyte

IF
InParty(Myself)
AreaCheck("AR1010")
Global("BEArenQuest","GLOBAL",4)
Global("BEBrysneSpawn","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("BEBrysneSpawn","GLOBAL",1)
CreateCreature("BEBRYSNE",[1045.965],13)
END

// amulet-medallion exchange

IF
InParty(Myself)
Global("BEArenQuest","GLOBAL",6)
PartyHasItem("AMUL14")
THEN
RESPONSE #100
SetGlobal("BEArenQuest","GLOBAL",7)
TakePartyItem("AMUL14")
DestroyItem("AMUL14")
CreateItem("BEMEDAL",0,0,0)
END

// Thayze grants xp

IF
InParty(Myself)
Global("BEArenQuest","GLOBAL",9)
THEN
RESPONSE #100
SetGlobal("BEArenQuest","GLOBAL",10)
AddXPObject(Player1,10000)
AddXPObject(Player2,10000)
AddXPObject(Player3,10000)
AddXPObject(Player4,10000)
AddXPObject(Player5,10000)
AddXPObject(Player6,10000)
PlaySound("EFF_M09")
CreateVisualEffect("SPSUNRAY",[360.302])
Wait(1)
CreateCreature("BESELEM2",[360.302],13)
END

IF
InParty(Myself)
Global("BEArenQuest","GLOBAL",11)
THEN
RESPONSE #100
SetGlobal("BEArenQuest","GLOBAL",12)
CreateItem("BEMEDAL2",0,0,0)
END

// Edwina-related banter
IF
  InParty(Myself)
  InParty("Edwin")
  !See([ENEMY])
  !Gender("Edwin",MALE)
  Global("BEEdwina","GLOBAL",0)
THEN
    RESPONSE #100
    RealSetGlobalTimer("BEEdwinaTimer","GLOBAL",300)
    SetGlobal("BEEdwina","GLOBAL",1)
END

IF
  InParty(Myself)
  InParty("Edwin")
  !See([ENEMY])
  !Gender("Edwin",MALE)
  RealGlobalTimerExpired("BEEdwinaTimer","GLOBAL")
  Global("BEEdwina","GLOBAL",1)
THEN
    RESPONSE #100
    SetGlobal("BEEdwina","GLOBAL",2)
END

IF
  InParty(Myself)
  InParty("Edwin")
  !See([ENEMY])
  Global("BEEdwin1","GLOBAL",2)
THEN
    RESPONSE #100
    Interact("BEAren")
END

IF
  InParty(Myself)
  InParty("Edwin")
  !See([ENEMY])
  Gender("Edwin",MALE)
  Global("BEEdwina","GLOBAL",3)
THEN
    RESPONSE #100
    RealSetGlobalTimer("BEEdwinaTimer","GLOBAL",300)
    SetGlobal("BEEdwina","GLOBAL",4)
END

IF
  InParty(Myself)
  InParty("Edwin")
  !See([ENEMY])
  RealGlobalTimerExpired("BEEdwinaTimer","GLOBAL")
  Global("BEEdwina","GLOBAL",4)
THEN
    RESPONSE #100
    SetGlobal("BEEdwina","GLOBAL",5)
END

IF
  InParty(Myself)
  InParty("Edwin")
  !See([ENEMY])
  Global("BEEdwin1","GLOBAL",2)
THEN
    RESPONSE #100
    Interact("BEAren")
END